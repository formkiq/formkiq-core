
description = "Lambda API to Dynamodb"

dependencies {

	annotationProcessor group: 'com.formkiq', name: 'graalvm-annotations-processor', version: '1.1.1'
	
	implementation project(':aws-s3')
	implementation project(':aws-sqs')
	implementation project(':aws-ssm')
	implementation project(':dynamodb-documents')
	
	implementation group: 'com.amazonaws', name: 'aws-lambda-java-core', version: '1.2.1'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.9.0'
    
    implementation group: 'com.formkiq', name: 'lambda-runtime-graalvm', version:'2.3.1'
    implementation group: 'com.formkiq', name: 'graalvm-annotations', version: '1.0.0'
    
	implementation group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.30'

	testImplementation project(':aws-cognito')
	testImplementation project(':aws-iam')
	testImplementation project(':aws-sts')
	testImplementation group: 'software.amazon.awssdk', name: 'apigateway', version: '2.17.167'
	testImplementation group: 'software.amazon.awssdk', name: 'sts', version: '2.17.167'
	
	testImplementation group: 'com.formkiq.stacks', name: 'client', version:'1.6.1'
    testImplementation group: 'junit', name: 'junit', version:'4.+'
}

compileJava {
    options.annotationProcessorPath += configurations.runtimeClasspath
}

nativeImage {
	outputFileName = "server"
	javaVersion = "java11"
	imageVersion = "21.0.0"
	mainClassName = "com.formkiq.lambda.runtime.graalvm.LambdaRuntime"
    enableHttp = true
    enableHttps = true
    enableDocker = true
	reflectionConfig = "$buildDir/classes/java/main/META-INF/graal/reflect.json"
	systemProperty = ["java.net.preferIPv4Stack=true"]
}

test {
  failFast = true
  exclude 'com/formkiq/stacks/api/awstest/**'
  
  doFirst {
	macroLocalStackStart()
	macroDynamoDbStart()
  }
  
  doLast {
	macroDockerComposeDown()
  } 
}

task testaws(type: Test) {
  description = 'Runs AWS integration tests.'
  include 'com/formkiq/stacks/api/awstest/**'
  outputs.upToDateWhen {false}
}

task buildZip(type: Zip) {
	dependsOn graalvmNativeImage
	inputs.files("${project.projectDir}/runtime/bootstrap", "${buildDir}/graalvm/server")
  	outputs.file("${buildDir}/lambda-api-graalvm.zip")
    
	archiveFileName = "lambda-api-graalvm.zip"
    destinationDirectory = file("${buildDir}")
    from("${project.projectDir}/runtime") {
        include 'bootstrap'
    }
    from("${buildDir}/graalvm") {
        include 'server'
    }
}

task buildJava11Zip(type: Zip) {
    from compileJava
    into('lib') {
        from configurations.default
    }           
}

task assembleTemplate {
	dependsOn buildZip
	inputs.files("src/main/resources/cloudformation/template-snippet.yaml", "src/main/resources/cloudformation/api.yaml", "src/main/resources/cloudformation/api-iam.yaml")
	outputs.dir("${buildDir}/distributions/formkiq-core")
	doLast {
		exec {
			commandLine "bash", "-c", "mkdir -p ${buildDir}/distributions/formkiq-core/sam/api && mkdir -p ${buildDir}/distributions/formkiq-core/sar/api"
		}
		exec {
			commandLine "bash", "-c", "cp ${buildDir}/lambda-api-graalvm.zip ${buildDir}/distributions/formkiq-core/sam/api/"
		}
		// build SAR distribution
		exec {
			commandLine "bash", "-c", "yq m --overwrite src/main/resources/cloudformation/template-snippet.yaml src/main/resources/cloudformation/api.yaml src/main/resources/cloudformation/api-iam.yaml > ${buildDir}/template-sar.yaml"
		}
		// build SAM distribution
		exec {
			commandLine "bash", "-c", "yq d ${buildDir}/template-sar.yaml 'Metadata' > ${buildDir}/distributions/formkiq-core/sam/api/template.yaml"
		}
		// build SAR distribution
		exec {
			commandLine "bash", "-c", "sam package --template-file ${buildDir}/template-sar.yaml --s3-bucket ${distrobucket} --region ${testregion} --profile ${testprofile} --output-template-file ${buildDir}/distributions/formkiq-core/sar/api/template.yaml"
		}
	}
}

task assembleOpenApiTemplate {
	inputs.files("src/main/resources/cloudformation/api.yaml", "src/main/resources/cloudformation/api-iam.yaml", "src/main/resources/cloudformation/api-variables.yaml")
	outputs.files("${project.projectDir}/openapi-jwt.yaml", "${project.projectDir}/openapi-iam.yaml")
	doLast {
		exec {
			commandLine "bash", "-c", "yq m --overwrite src/main/resources/cloudformation/api.yaml src/main/resources/cloudformation/api-variables.yaml > ${buildDir}/openapi-jwt.yaml"
		}
		exec {
			commandLine "bash", "-c", "tail -n +6 ${buildDir}/openapi-jwt.yaml > ${buildDir}/../../openapi-jwt.yaml"
		}
		exec {
			commandLine "bash", "-c", "openapi-generator generate -i ${buildDir}/../../openapi-jwt.yaml -g markdown -o ${buildDir}/openapi-jwt"
		}
		exec {
			commandLine "bash", "-c", "yq m --overwrite src/main/resources/cloudformation/api-iam.yaml src/main/resources/cloudformation/api-iam-variables.yaml > ${buildDir}/openapi-iam.yaml"
		}
		exec {
			commandLine "bash", "-c", "tail -n +6 ${buildDir}/openapi-iam.yaml > ${buildDir}/../../openapi-iam.yaml"
		}
		exec {
			commandLine "bash", "-c", "openapi-generator generate -i ${buildDir}/../../openapi-iam.yaml -g markdown -o ${buildDir}/openapi-iam"
		}
	}
}

task downloadAwsCognito(type: Download) {
    src "https://github.com/formkiq/aws-cognito/releases/download/v${awsCognitoVersion}/aws-cognito-v${awsCognitoVersion}.zip"
    dest buildDir
    overwrite false
}
/*
task downloadAwsCognito(type: Copy) {
    from file("$buildDir/../../../aws-cognito/build/aws-cognito-v${awsCognitoVersion}.zip")
    into file("${buildDir}")
}*/

task unzipAwsCognito(type: Copy) {
  dependsOn downloadAwsCognito
  def zipFile = file("${buildDir}/aws-cognito-v${awsCognitoVersion}.zip")
  def outputDir = file("${buildDir}/distributions/formkiq-core/sam/cognito")

  from zipTree(zipFile)
  into outputDir
}

buildJava11Zip.dependsOn jar

graalvmNativeImage.dependsOn check
build.dependsOn assembleTemplate, assembleOpenApiTemplate, unzipAwsCognito
