description = "FormKiQ Core - Lambda S3 to Dynamodb"

sourceSets {
  integration {
    java.srcDir "$projectDir/src/integration/java"
    resources.srcDir "$projectDir/src/integration/resources"

    compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
    runtimeClasspath += output + compileClasspath
  }
}

configurations {
  integrationImplementation.extendsFrom testImplementation
  integrationRuntimeOnly.extendsFrom testRuntimeOnly
}

dependencies {

  annotationProcessor group: 'com.formkiq', name: 'graalvm-annotations-processor', version: '1.5.2'

  implementation project(':aws-s3')
  implementation project(':aws-sns')
  implementation project(':aws-ssm')
  implementation project(':aws-ses')
  implementation project(':aws-eventbridge')
  implementation project(':aws-dynamodb')
  implementation project(':fkq-plugins')
  implementation project(':dynamodb-documents')
  implementation project(':document-events')
  implementation project(':actions')
  implementation project(':fkq-lambda-services')
  implementation project(':fkq-validation')
  implementation project(':typesense')
  implementation project(':http')
  implementation project(':http-sigv4')
  implementation project(':strings')

  // remove at some point and replace with custom light-weight records
  implementation group: 'com.amazonaws', name: 'aws-lambda-java-core', version: '1.4.0'
  implementation group: 'com.google.code.gson', name: 'gson', version: '2.13.2'
  implementation group: 'net.coobird', name: 'thumbnailator', version: '0.4.21'

  implementation group: 'com.formkiq', name: 'lambda-runtime-graalvm', version:'2.6.0'
  implementation group: 'com.formkiq', name: 'graalvm-annotations', version: '1.2.0'

  implementation group: 'org.slf4j', name: 'slf4j-simple', version: '2.0.17'

  testImplementation project(':aws-sqs')
  testImplementation project(':fkq-test-utils')
  testImplementation project(':fkq-validation')
  testImplementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.20.0'

  testImplementation group: 'software.amazon.awssdk', name: 'dynamodb', version: '2.40.14'
  testImplementation group: 'software.amazon.awssdk', name: 's3', version: '2.40.14'
  testImplementation group: 'software.amazon.awssdk', name: 'iam', version: '2.40.14'

  testImplementation platform("org.junit:junit-bom:6.0.1")
  testImplementation "org.junit.jupiter:junit-jupiter"
  testRuntimeOnly  "org.junit.platform:junit-platform-launcher"

  testImplementation platform("org.testcontainers:testcontainers-bom:2.0.3")
  testImplementation "org.testcontainers:testcontainers"
  testImplementation "org.testcontainers:testcontainers-junit-jupiter"
  testImplementation "org.testcontainers:testcontainers-localstack"

  testImplementation group: 'org.mock-server', name: 'mockserver-netty', version: '5.15.0'
}

compileJava {
  options.annotationProcessorPath += configurations.runtimeClasspath
}

tasks.register("integrationTest", Test) {
  testClassesDirs = sourceSets.integration.output.classesDirs
  classpath = sourceSets.integration.runtimeClasspath
}
