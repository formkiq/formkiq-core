description = "FormKiQ Core - Lambda S3 to Dynamodb"

dependencies {

 	annotationProcessor group: 'com.formkiq', name: 'graalvm-annotations-processor', version: '1.0.0'
 
	implementation project(':aws-s3')
	implementation project(':aws-sns')
	implementation project(':aws-sqs')
	implementation project(':aws-ssm')
	implementation project(':dynamodb-documents')
	
	implementation group: 'com.amazonaws', name: 'aws-lambda-java-core', version: '1.2.1'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.6'
    
	implementation group: 'com.formkiq', name: 'lambda-runtime-graalvm', version:'2.1'
    implementation group: 'com.formkiq', name: 'graalvm-annotations', version: '1.0.0'
    
	implementation group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.30'
	
    testImplementation group: 'junit', name: 'junit', version:'4.+'
    testImplementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.11'

	testImplementation group: 'software.amazon.awssdk', name: 'dynamodb', version: '2.15.25'
	testImplementation group: 'software.amazon.awssdk', name: 's3', version: '2.15.25'
}

compileJava {
    options.annotationProcessorPath += configurations.runtimeClasspath
}

nativeImage { 
	outputFileName = "server"
	javaVersion = "java11"
	imageVersion = "20.3.0"
	mainClassName = "com.formkiq.lambda.runtime.graalvm.LambdaRuntime"
    enableHttp = true
    enableHttps = true
    enableDocker = true
	reflectionConfig = "$buildDir/classes/java/main/META-INF/graal/reflect.json"
	systemProperty = ["java.net.preferIPv4Stack=true"]
}

test {
  failFast = true
  exclude 'com/formkiq/stacks/lambda/s3/awstest/**'
  
  doFirst {
    macroLocalStackStart()
	macroDynamoDbStart()
  }
  
  doLast {
	macroDockerComposeDown()
  }  
}

task testaws(type: Test) {
  description = 'Runs AWS integration tests.'
  outputs.upToDateWhen {false}
  include 'com/formkiq/stacks/lambda/s3/awstest/**'    
}

task buildZip(type: Zip) {
	inputs.files("${project.projectDir}/runtime/bootstrap", "${buildDir}/graalvm/server")
  	outputs.dir("${buildDir}/distributions")
  	
	archiveName "lambda-s3-graalvm.zip"
    destinationDir(file("${buildDir}/distributions"))
    from("${project.projectDir}/runtime") {
        include 'bootstrap'
    }
    from("${buildDir}/graalvm") {
        include 'server'
    }
}

task uploadToS3 {
  inputs.files("${buildDir}/distributions/lambda-s3-graalvm.zip")
  outputs.dir("${buildDir}/distributions")
  doLast {
	exec {
	  commandLine "bash", "-c", "aws s3 cp ${buildDir}/distributions/lambda-s3-graalvm.zip s3://${distrobucket}/formkiq-core/${project.version}/lambda-s3-graalvm.zip --profile ${testprofile}"
	}
  }
}

task assembleTemplate {
    dependsOn buildZip
	inputs.files("template-sar.yaml")
	outputs.files("build/formkiq-core.yaml", "build/formkiq-core-sar.yaml")
	doLast {
		// build SAM distribution
		exec {
			commandLine "bash", "-c", "yq d template-sar.yaml 'Metadata' > template.yaml"
		}	
		// build SAR distribution
		exec {
			commandLine "bash", "-c", "sam package --template-file template-sar.yaml --s3-bucket ${distrobucket} --region ${testregion} --profile ${testprofile} --output-template-file build/formkiq-core-sar.yaml"
		}
	}
}

task buildDockerImage {
  dependsOn buildZip
  inputs.files("Dockerfile", "src/main")
  outputs.dir("${buildDir}/distributions")
  doLast {
	exec {
	  commandLine "bash", "-c", "docker build -t formkiq-core-lambda-s3 ."
	}
  }
}

buildZip.dependsOn graalvmNativeImage
graalvmNativeImage.dependsOn check
uploadToS3.dependsOn buildZip

build.dependsOn uploadToS3, assembleTemplate, buildDockerImage
