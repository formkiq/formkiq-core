description = "FormKiQ Core - Lambda S3 Graalvm"

apply plugin: 'distribution'

dependencies {
  implementation project(':lambda-s3')
}

nativeImage {
  dockerFile = "${project.projectDir}/Dockerfile"
}

tasks.distTar.enabled = false

distZip {
  archiveFileName = "storage/lambda-s3-graalvm.zip"
}

distributions {
  main {
    contents {
      from("runtime/") {
        include "bootstrap"
      }
      from ("${buildDir}/graalvm") {
        include '**/**'
      }
      into '/'
    }
  }
}

tasks.register('buildZip', Zip) {
  archiveFileName = "lambda-s3-graalvm.zip"
  destinationDirectory = layout.buildDirectory.dir('dist')
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE

  from("${project.projectDir}/lib") {
    include '**/**'
  }
  from("${project.projectDir}/runtime") {
    include 'bootstrap'
  }
  from("build/graalvm") {
    include '**/**'
  }
}

tasks.register('copyRuntimeClasspath', Copy) {
  description = 'Copies all runtime classpath dependencies to build/libs'
  from({ configurations.runtimeClasspath })
  into(layout.buildDirectory.dir('libs'))
}

jar.dependsOn(copyRuntimeClasspath)

tasks.register('unzipFile', Copy) {
  from({
    zipTree(layout.buildDirectory.file("graalvm/output/graalvm.zip").get().asFile)
  })
  into(layout.buildDirectory.dir("graalvm"))
}

graalvmNativeImage.dependsOn("jar")

unzipFile.dependsOn graalvmNativeImage
buildZip.dependsOn unzipFile
distZip.dependsOn buildZip
