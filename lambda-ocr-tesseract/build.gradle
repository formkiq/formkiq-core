description = "FormKiQ Core - Lambda OCR Tesseract"

apply plugin: 'distribution'

def moduleName = "formkiq-module-lambda-ocr-tesseract"

dependencies {

  implementation project(':actions')
  implementation project(':document-events')
  implementation project(':aws-s3')
  implementation project(':aws-sqs')
  implementation project(':aws-sns')
  implementation project(':aws-dynamodb')
  implementation project(':ocr')
  implementation project(':fkq-lambda-services')
  implementation project(':fkq-lambda-core')
  implementation("net.sourceforge.tess4j:tess4j:5.16.0")
  implementation("org.slf4j:slf4j-simple:2.0.17")
  implementation group: 'com.amazonaws', name: 'aws-lambda-java-core', version: '1.3.0'
  implementation group: 'com.google.code.gson', name: 'gson', version: '2.13.1'
  implementation group: 'net.java.dev.jna', name: 'jna', version: '5.17.0'

  implementation group: 'org.apache.poi', name: 'poi-ooxml', version: '5.4.1'
  implementation group: 'org.apache.poi', name: 'poi-scratchpad', version: '5.4.1'

  testImplementation project(':fkq-test-utils')

  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version:'5.11.4'
  testImplementation group: 'org.testcontainers', name: 'testcontainers', version: '1.21.1'
  testImplementation group: 'org.testcontainers', name: 'junit-jupiter', version: '1.21.1'
  testImplementation group: 'org.testcontainers', name: 'localstack', version: '1.21.1'
}

test {
  failFast = true
  useJUnitPlatform()
  maxHeapSize = '2G'
}

tasks.register('downloadZipFile', Download) {
  src('https://github.com/bweigel/aws-lambda-tesseract-layer/releases/download/v5.3.9/tesseract-al2-x86.zip')
  dest(layout.buildDirectory.file('tesseract-al2-x86.zip'))
  overwrite(false)
}

tasks.register('downloadAndUnzipFile', Copy) {
  dependsOn(tasks.named('downloadZipFile'))
  from({ zipTree(layout.buildDirectory.file('tesseract-al2-x86.zip')) })
  into(layout.buildDirectory.dir('tesseract'))
}

tasks.register('buildTesseractLayer', Zip) {
  dependsOn 'downloadAndUnzipFile'
  archiveFileName = 'layer-tesseract-5.3.9.zip'
  destinationDirectory = layout.buildDirectory.dir('distributions/api')

  from(layout.buildDirectory.dir('tesseract')) { spec ->
    eachFile { details ->
      if (details.path.matches('tesseract/lib/libtesseract\\.so\\..*')) {
        details.path = 'tesseract/lib/libtesseract.so'
      }
    }
    includeEmptyDirs = false

    // 2) keep only eng/osd traineddata; drop the rest
    exclude { details ->
      if (details.path.startsWith('tesseract/tesseract/share/tessdata/')) {
        def name = details.file.name
        return !(name in [
          'eng.traineddata',
          'osd.traineddata'
        ])
      }
      return false
    }
  }
}

distZip.dependsOn buildTesseractLayer

tasks.distTar.enabled = false

distZip {
  archiveFileName = "api/${moduleName}.zip"
}

distributions {
  main {
    contents {
      from compileJava
      from processResources
      into('lib') {
        from configurations.runtimeClasspath
      }
      into '/'
    }
  }
}
