import org.apache.tools.ant.taskdefs.condition.Os
description = "FormKiQ Core - WebSocket"

sourceSets {
    integration {
        java.srcDir "$projectDir/src/integration/java"
        resources.srcDir "$projectDir/src/integration/resources"
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

configurations {
    integrationImplementation.extendsFrom testImplementation
    integrationRuntime.extendsFrom testRuntime
}

def getCmd() {
    String os = System.getProperty("os.name").toLowerCase()
    return os.contains("win") ? "cmd" : "bash"
}

def getCmdParam() {
    String os = System.getProperty("os.name").toLowerCase()
    return os.contains("win") ? "/c" : "-c"
}

jar.enabled = false

dependencies {
	testImplementation project(':aws-sqs')
	testImplementation project(':aws-ssm')
        testImplementation project(':aws-cognito-identity')
        testImplementation project(':aws-cognito-identityprovider')
	testImplementation project(':aws-dynamodb')
	testImplementation project(':dynamodb-documents')
	
	testImplementation group: 'software.amazon.awssdk', name: 'dynamodb', version: '2.20.140'
	
	testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version:'5.10.0'
    testImplementation group: 'com.google.code.gson', name: 'gson', version: '2.10.1'
	testImplementation group: 'org.java-websocket', name: 'Java-WebSocket', version: '1.5.3'
	testImplementation group: 'org.slf4j', name: 'slf4j-simple', version: '2.0.7'
	testImplementation group: 'com.formkiq.stacks', name: 'client', version:'1.11.0'
}

test {
  failFast = true 
  useJUnitPlatform()
}

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integration.output.classesDirs
    classpath = sourceSets.integration.runtimeClasspath
    useJUnitPlatform()
}

task npmInstall {
  inputs.files("src/main/resources/js")
  outputs.files("${buildDir}/resources/main/js/websocket-api.zip")
  dependsOn processResources, compileTestJava, checkstyleTest, spotbugsTest, compileIntegrationJava, checkstyleIntegration, spotbugsIntegration
  
  doLast {
	  
	  exec {
	    workingDir "${buildDir}/resources/main/js"
		commandLine getCmd(), getCmdParam(), "npm install"
	  }
	  
	  exec {
	    workingDir "${buildDir}/resources/main/js"
		commandLine getCmd(), getCmdParam(), "npm run pack"
	  }
  }
}

jar.dependsOn npmInstall

task npmtest {
  onlyIf { !Os.isFamily(Os.FAMILY_WINDOWS) }
  dependsOn npmInstall, jar
  inputs.files("src/main/resources/js")
  outputs.dir("${buildDir}/resources/main/js/coverage")
  doLast {
    exec {
      commandLine getCmd(), getCmdParam(), "cd build/resources/main/js ; npm test"
    }
  }
}

task assembleTemplate {
    dependsOn check
	inputs.files("src/main/resources/cloudformation/template.yaml")
	outputs.dir("${buildDir}/distributions/formkiq-core")
	doLast {
	
		copy {
		  from layout.buildDirectory.file("${buildDir}/resources/main/js/websocket-api.zip"), layout.projectDirectory.file("src/main/resources/cloudformation/template.yaml")
	      into "${buildDir}/distributions/formkiq-core/sam/websocket-api"
	    }
	}
}

task updateLambda {
	outputs.upToDateWhen { false }
	dependsOn assembleTemplate
	doLast {
		exec {
			commandLine getCmd(), getCmdParam(), "aws lambda list-functions --region ${testregion} --profile ${testprofile} | jq -r '.Functions[] | .FunctionName' | grep formkiq-core-${testappenvironment} | grep 'Websocket' | xargs -I{} aws lambda update-function-code --function-name {} --zip-file fileb://${buildDir}/websocket-api.zip --region ${testregion} --profile ${testprofile}"
		}
	}
}

ext.macroLocalStackStart = {
	exec {
	  commandLine getCmd(), getCmdParam(), "docker-compose  -f ./docker-compose.yml up -d"
	}
	exec {
	  commandLine getCmd(), getCmdParam(), "${project.rootDir}/wait-for-localstack.sh 4568"
	}
}
  
ext.macroDockerComposeDown = {
    exec {
      commandLine getCmd(), getCmdParam(), "docker-compose  -f ${project.rootDir}/docker-compose.yml down"
    }
}

test.dependsOn npmtest
build.dependsOn assembleTemplate
