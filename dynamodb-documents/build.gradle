
description = "DocumentStore Dynamodb"

sourceSets {
    integration {
        java.srcDir "$projectDir/src/integration/java"
        resources.srcDir "$projectDir/src/integration/resources"
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

configurations {
    integrationImplementation.extendsFrom testImplementation
    integrationRuntime.extendsFrom testRuntime
}

dependencies {

	annotationProcessor group: 'com.formkiq', name: 'graalvm-annotations-processor', version: '1.4.1'

	implementation project(':aws-dynamodb')
	implementation project(':document-events')
	implementation project(':fkq-plugins')
	implementation project(':fkq-validation')
	implementation project(':fkq-lambda-services')
    implementation group: 'com.formkiq', name: 'graalvm-annotations', version: '1.2.0'
    
	testImplementation project(':aws-ssm')
	testImplementation project(':fkq-test-utils')
	testImplementation 'org.slf4j:slf4j-api:2.0.7'
	testImplementation group: 'com.google.code.gson', name: 'gson', version: '2.10.1'
	testImplementation group: 'com.google.guava', name: 'guava', version: '31.1-jre'
	testImplementation group: 'org.slf4j', name: 'slf4j-simple', version: '2.0.7'
	
	testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version:'5.10.1'
    testImplementation group: 'org.testcontainers', name: 'testcontainers', version: '1.19.4'
	testImplementation group: 'org.testcontainers', name: 'junit-jupiter', version: '1.19.4'
	testImplementation group: 'com.amazonaws', name: 'aws-java-sdk', version: '1.12.646'
	
	configurations.all {
   		exclude group: 'software.amazon.awssdk', module: 'apache-client'
   		exclude group: 'software.amazon.awssdk', module: 'netty-nio-client'
	}
}

jar {
    archiveBaseName = 'dynamodb-documents'
}

task createZip(type: Zip) {   
   from "nodejs"
   include "*"
   include "*/*"
   archiveFileName = "documentsmetrics-${project.version}.zip"
   destinationDirectory = file("$buildDir/distributions")
}

createZip.dependsOn check
build.dependsOn createZip

test {
  failFast = true
  useJUnitPlatform() 
}

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integration.output.classesDirs
    classpath = sourceSets.integration.runtimeClasspath
    useJUnitPlatform()
}
