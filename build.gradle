
plugins {
  id 'java-library'
  id 'checkstyle'
  id 'com.github.spotbugs'  version '4.6.0'
  id 'com.diffplug.spotless' version '5.8.2'
  id 'com.github.ben-manes.versions' version '0.36.0'
  id 'com.formkiq.gradle.graalvm-native-plugin' version '1.0.3'
}

spotless { 
  java { 
    eclipse().configFile project.rootProject.file("spotless.eclipseformat.xml")
    licenseHeaderFile project.rootProject.file("LICENSE")  
  } 
}

allprojects {
  version = '1.1.0'
  group = 'com.formkiq.stacks'
}

subprojects {

  repositories {
  	mavenLocal()
    mavenCentral()
  }

  apply plugin: 'java-library'
  apply plugin: 'checkstyle'
  apply plugin: 'com.github.spotbugs'
  apply plugin: 'com.diffplug.spotless'
  apply plugin: 'com.github.ben-manes.versions'
  apply plugin: 'com.formkiq.gradle.graalvm-native-plugin'
  
  sourceCompatibility = "11"
  targetCompatibility = "11"
  
  compileJava.dependsOn 'spotlessCheck'

  tasks.withType(Test) {
    systemProperties['testregion'] = project.getProperty('testregion')
    systemProperties['testprofile'] = project.getProperty('testprofile')
    systemProperties['testappenvironment'] = project.getProperty('testappenvironment')
  }

  spotlessJavaCheck.dependsOn 'spotlessJavaApply'

  spotbugs {
    excludeFilter = file("$rootDir/config/gradle/spotbugs-exclude.xml")
  }
     
  spotbugsMain {
    reports {
        html {
            enabled = true
        }
    }
  } 
  
  spotbugsTest {
    reports {
        html {
            enabled = true
        }
    }
  } 
  
  checkstyle {
    toolVersion '8.29'
    configFile file("config/checkstyle/checkstyle.xml")
    configProperties = [project_loc: "${projectDir}"]
  }

  tasks.withType(Checkstyle).each { checkstyleTask ->
    checkstyleTask.doLast {
        reports.all { report ->
            def outputFile = report.destination
            if (outputFile.exists() && outputFile.text.contains("<error ")) {
                throw new GradleException("There were checkstyle warnings! For more info check $outputFile")
            }
        }
    }
  }
  
  ext.macroLocalStackStart = {
	exec {
	  commandLine "bash", "-c", "docker-compose  -f ${project.rootDir}/docker-compose.yml up -d localstack"
	}
	exec {
	  commandLine "bash", "-c", "${project.rootDir}/wait-for-localstack.sh"
	}
  }
  
  ext.macroDynamoDbStart = {
  	exec {
	  commandLine "bash", "-c", "docker-compose  -f ${project.rootDir}/docker-compose.yml up -d dynamodb"
	}
	exec {
	  commandLine "bash", "-c", "${project.rootDir}/wait-for-dynamodb.sh"
	}
  }
  
  ext.macroDynamoDbCreateTables = {
	exec {
	  commandLine "bash", "-c", "for f in \$(find ${project.rootDir}/dynamodb-documents/src/test/resources -name \"*.json\"); do aws dynamodb create-table --region ${awsregion} --endpoint=${dynamodbEndpoint} --profile ${awsprofile} --cli-input-json \"\$(cat \$f)\"; done"
	}
  }
  
  ext.macroDynamoDbDeleteAllTables = {
    exec {
      commandLine "bash", "-c", "aws dynamodb list-tables --endpoint=${dynamodbEndpoint} --profile ${awsprofile} --region ${awsregion} | jq -r '.TableNames[]' | xargs -I {} aws dynamodb delete-table --table-name {} --region ${awsregion} --profile ${awsprofile} --endpoint=${dynamodbEndpoint}"
    }
  }
  
  ext.macroDockerComposeDown = {
    exec {
      commandLine "bash", "-c", "docker-compose  -f ${project.rootDir}/docker-compose.yml down"
    }
  }

  task dockerComposeUp {
    doLast {
      macroLocalStackStart()
	  macroDynamoDbStart()
    }
  }

  task dockerComposeDown {
    doLast {
      macroDockerComposeDown()
    }
  }
}

description = "FormKiQ Core"

task testaws {
  dependsOn subprojects.collect { subproject ->
    subproject.tasks.matching { it.name == "testaws" }
  }
}

task assembleTemplate {
    dependsOn subprojects.build
	inputs.files("template-snippet.yaml", "console/template.yaml", "console/template-sar.yaml", "lambda-api/template-snippet.yaml", "lambda-api/api.yaml", "lambda-api/api-iam.yaml", "lambda-s3/template.yaml", "lambda-s3/template-sar.yaml", "module-email-notify/template.yaml", "module-email.notify/template-sar.yaml")
  	outputs.files("template.yaml", "build/formkiq-core.yaml")
	doLast {
		// build SAM distribution
		exec {
			commandLine "bash", "-c", "sam package --template-file template.yaml --s3-bucket ${distrobucket} --region ${testregion} --profile ${testprofile} --output-template-file build/formkiq-core.yaml"
		}
		// build SAR template
		exec {
			commandLine "bash", "-c", "yq m --overwrite ./build/formkiq-core.yaml ./template-sar-snippet.yaml > ./template.sar.yaml"
		}
		// build SAR distribution
		exec {
			commandLine "bash", "-c", "sam package --template-file ./template.sar.yaml --s3-bucket ${distrobucket} --region ${testregion} --profile ${testprofile} --output-template-file build/formkiq-core-sar.yaml"
		}		
		exec {
			commandLine "bash", "-c", "box tokenReplace path=build/*.yaml token=\"{{version}}\" replacement=\"${project.version}\""
		}
		exec {
			commandLine "bash", "-c", "box tokenReplace path=build/*.yaml token=\"{{aws_accountid}}\" replacement=\"${aws_account_id}\""
		}
	}
}

check.dependsOn ':assembleTemplate'

task deploy {
    dependsOn assembleTemplate
    outputs.upToDateWhen { false }
	doLast {
		exec {
			commandLine "bash", "-c", "sam deploy --template-file build/formkiq-core.yaml --stack-name formkiq-core-${testappenvironment} --parameter-overrides ParameterKey=AppEnvironment,ParameterValue=${testappenvironment} ParameterKey=AdminEmail,ParameterValue=${testadminemail} ParameterKey=EnablePublicUrls,ParameterValue=true --capabilities CAPABILITY_IAM CAPABILITY_AUTO_EXPAND CAPABILITY_NAMED_IAM --region ${testregion} --profile ${testprofile}"
		}
	}
}

task undeploy {
	outputs.upToDateWhen { false }
	doLast {
	  	def group = new ByteArrayOutputStream()

  		// find ApiGatewayInvokeGroup Name
  		exec {
			commandLine "bash", "-c", "aws iam list-groups --profile ${testprofile} | jq -r '.Groups[].GroupName' | grep '^formkiq-core-${testappenvironment}'"
			standardOutput = group
			ignoreExitValue = true
  		}
  		// remove any temp queues.
  		exec {
			commandLine "bash", "-c", "aws sqs list-queues --region ${testregion} --profile ${testprofile} | jq -r '.QueueUrls[]' | grep '/createtest' | xargs -I{} aws sqs delete-queue --queue-url {} --region ${testregion} --profile ${testprofile}"
			ignoreExitValue = true
  		}
  		// remove all users in ApiGatewayInvokeGroup
  		exec {
			commandLine "bash", "-c", "aws iam get-group --group-name ${group.toString().trim()} --profile ${testprofile} | jq -r '.Users[].UserName' | xargs -I{} aws iam remove-user-from-group --group-name ${group.toString().trim()} --user-name {} --region ${testregion} --profile ${testprofile}"
			ignoreExitValue = true
  		}
		exec {
			commandLine "bash", "-c", "aws cloudformation delete-stack --stack-name formkiq-core-${testappenvironment} --region ${testregion} --profile ${testprofile}"
		}
		// delete dynamodb tables
		exec {
			commandLine "bash", "-c", "aws dynamodb list-tables --region ${testregion} --profile ${testprofile} | jq -r '.TableNames[]' | grep '^formkiq-core-${testappenvironment}' | xargs -I {} aws dynamodb delete-table --table-name {} --region ${testregion} --profile ${testprofile}"
		}
		// disable s3 bucket versioning
		exec {
			commandLine "bash", "-c", "aws s3api list-buckets --query \"Buckets[].Name\" --profile ${testprofile} | jq -r '.[]' | grep '^formkiq-core-${testappenvironment}' | xargs -I{} aws s3api put-bucket-versioning --bucket {} --versioning-configuration Status=Suspended --region ${testregion} --profile ${testprofile}"
  		}
  		// delete all s3 objects
  		exec {
	  		commandLine "bash", "-c", "aws s3api list-buckets --query \"Buckets[].Name\" --profile ${testprofile} | jq -r '.[]' | grep '^formkiq-core-${testappenvironment}' | xargs -I {} python3 ${project.projectDir}/emptyVersionedBucket.py -b {} -p ${testprofile}"
	  	}
  		// delete s3 buckets
		exec {
			commandLine "bash", "-c", "aws s3api list-buckets --query \"Buckets[].Name\" --profile ${testprofile} | jq -r '.[]' | grep '^formkiq-core-${testappenvironment}' | xargs -I{} aws s3 rb s3://{} --force --region ${testregion} --profile ${testprofile}"
  		}
  		// delete cloudwatch logs
		exec {
			commandLine "bash", "-c", "aws logs describe-log-groups --log-group-name-prefix /aws/lambda/formkiq-core-${testappenvironment} --region ${testregion} --profile ${testprofile}  | jq -r '.logGroups[].logGroupName' | xargs -I{} aws logs delete-log-group --log-group-name {} --region ${testregion} --profile ${testprofile}"
  		}
  		// wait for stack to be deleted
  		exec {
	  		commandLine "bash", "-c", "aws cloudformation wait stack-delete-complete --stack-name formkiq-core-${testappenvironment} --region ${testregion} --profile ${testprofile}"
		}
	}
}

task createSar {
    dependsOn build
    outputs.upToDateWhen { false }
	doLast {
		exec {
			commandLine "bash", "-c", "sam publish --template-file lambda-s3/build/formkiq-core-sar.yaml --semantic-version ${project.version} --region ${testregion} --profile ${testprofile}"
		}
		exec {
			commandLine "bash", "-c", "sam publish --template-file lambda-api/build/formkiq-core-sar.yaml --semantic-version ${project.version} --region ${testregion} --profile ${testprofile}"
		}
		exec {
			commandLine "bash", "-c", "sam publish --template-file console/build/formkiq-core-sar.yaml --semantic-version ${project.version} --region ${testregion} --profile ${testprofile}"
		}
		exec {
			commandLine "bash", "-c", "sam publish --template-file module-email-notify/build/formkiq-core-sar.yaml --semantic-version ${project.version} --region ${testregion} --profile ${testprofile}"
		}
		exec {
			commandLine "bash", "-c", "sam publish --template-file build/formkiq-core-sar.yaml --semantic-version ${project.version} --region ${testregion} --profile ${testprofile}"
		}
	}
}

task deleteSar {
	outputs.upToDateWhen { false }
	doLast {
		exec {
			commandLine "bash", "-c", "aws serverlessrepo delete-application --application-id arn:aws:serverlessrepo:${testregion}:${aws_account_id}:applications/FormKiQ-Core-Storage --region ${testregion} --profile ${testprofile}"
			ignoreExitValue = true
		}
		exec {
			commandLine "bash", "-c", "aws serverlessrepo delete-application --application-id arn:aws:serverlessrepo:${testregion}:${aws_account_id}:applications/FormKiQ-Core-Api --region ${testregion} --profile ${testprofile}"
			ignoreExitValue = true
		}
		exec {
			commandLine "bash", "-c", "aws serverlessrepo delete-application --application-id arn:aws:serverlessrepo:${testregion}:${aws_account_id}:applications/FormKiQ-Core-Console --region ${testregion} --profile ${testprofile}"
			ignoreExitValue = true
		}
		exec {
			commandLine "bash", "-c", "aws serverlessrepo delete-application --application-id arn:aws:serverlessrepo:${testregion}:${aws_account_id}:applications/FormKiQ-Module-Email-Nodify --region ${testregion} --profile ${testprofile}"
			ignoreExitValue = true
		}
		exec {
			commandLine "bash", "-c", "aws serverlessrepo delete-application --application-id arn:aws:serverlessrepo:${testregion}:${aws_account_id}:applications/FormKiQ-Core --region ${testregion} --profile ${testprofile}"
			ignoreExitValue = true
		}		
	}
}