
plugins {
  id 'java-library'
  id 'checkstyle'
  id 'com.github.spotbugs'  version '5.0.14'
  id 'com.diffplug.spotless' version '6.17.0'
  id 'com.github.ben-manes.versions' version '0.46.0'
  id 'com.formkiq.gradle.graalvm-native-plugin' version '1.4.0'
  id 'de.undercouch.download' version '5.4.0'
}

def moduleName = "formkiq-core"

repositories { mavenCentral() }

allprojects {
  version = '1.11.0'
  ext.awsCognitoVersion = '1.4.6'
  group = 'com.formkiq.stacks'
  
	apply plugin: 'com.diffplug.spotless'
  
	java {
		toolchain {
			languageVersion.set(JavaLanguageVersion.of(11))
		}
	}
  
	spotless { 
	  java { 
	    eclipse().configFile project.rootProject.file("spotless.eclipseformat.xml")
	    licenseHeaderFile project.rootProject.file("LICENSE")  
	  } 
	}
	
	spotlessJavaCheck.dependsOn 'spotlessJavaApply'
	
}

subprojects {

  repositories {
  	mavenLocal()
    mavenCentral()
    maven {
      url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
  }

  apply plugin: 'java-library'
  apply plugin: 'checkstyle'
  apply plugin: 'com.github.spotbugs'
  apply plugin: 'com.github.ben-manes.versions'
  apply plugin: 'com.formkiq.gradle.graalvm-native-plugin'
  
  sourceCompatibility = "11"
  targetCompatibility = "11"
  
  compileJava.dependsOn 'spotlessCheck'

  tasks.withType(Test) {
    systemProperties['testregion'] = project.getProperty('testregion')
    systemProperties['testprofile'] = project.getProperty('testprofile')
    systemProperties['testappenvironment'] = project.getProperty('testappenvironment')
  }

  spotbugs {
    excludeFilter = file("$rootDir/config/gradle/spotbugs-exclude.xml")
  }
     
  spotbugsMain {
    reports {
        html {
            enabled = true
        }
    }
  } 
  
  spotbugsTest {
    reports {
        html {
            enabled = true
        }
    }
  } 
  
  checkstyle {
    toolVersion '8.29'
    configFile file("config/checkstyle/checkstyle.xml")
    configProperties = [project_loc: "${projectDir}"]
  }

  tasks.withType(Checkstyle).each { checkstyleTask ->
    checkstyleTask.doLast {
        reports.all { report ->
            def outputFile = report.destination
            if (outputFile.exists() && outputFile.text.contains("<error ")) {
                throw new GradleException("There were checkstyle warnings! For more info check $outputFile")
            }
        }
    }
  }
}

description = "FormKiQ Core"

task testaws {
  dependsOn subprojects.collect { subproject ->
    subproject.tasks.matching { it.name == "testaws" }
  }
}

task buildDistribution(type: Copy) {
	dependsOn subprojects.build
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
	outputs.upToDateWhen {false}

    from 'console/build/distributions/formkiq-core'
    from 'lambda-api/build/distributions/formkiq-core'
    from 'lambda-s3/build/distributions/formkiq-core'
    from 'lambda-typesense/build/distributions/formkiq-module-lambda-typesense'
    from 'lambda-ocr-tesseract/build/distributions/formkiq-core'
    from 'module-email-notify/build/distributions/formkiq-core'
    from 'websocket-api/build/distributions/formkiq-core'
    from 'lambda-apikey-authorizer/build/distributions/formkiq-core'
    into "${buildDir}/distributions/formkiq-core"
}

task assembleTemplate {
    dependsOn buildDistribution
  	outputs.upToDateWhen { false }
	doLast {
		exec {
			commandLine "bash", "-c", "cp src/main/resources/cloudformation/* ${buildDir}/distributions/formkiq-core/sam/"
		}
		exec {
			commandLine "bash", "-c", "ytt --data-value version=${project.version} -f ${buildDir}/distributions/formkiq-core/sam/template.yaml --output-files ${buildDir}/distributions/formkiq-core/sam"
		}
		exec {
			commandLine "bash", "-c", "cp src/main/resources/cloudformation/module-*.yaml ${buildDir}/distributions/formkiq-core/sam/"
		}	
	}
}

check.dependsOn ':buildSamZip'

/**
 * SAM Packges Uploads enterprise build to the customer's bucket.
 */
task uploadToCustomerBucket {
  dependsOn build
  outputs.upToDateWhen { false }
  doLast {
    exec {
      commandLine "bash", "-c", "sam package --s3-prefix ${project.version} --s3-bucket ${moduleName} --template-file ${buildDir}/distributions/${moduleName}/sam/template.yaml --region ${testregion} --output-template-file ${buildDir}/distributions/${moduleName}/sam/template-${project.version}.yaml --profile ${testprofile}"
    }
    exec {
      commandLine "bash", "-c", "aws s3 cp ${buildDir}/distributions/${moduleName}/sam/template-${project.version}.yaml s3://${moduleName}/${project.version}/template.yaml --only-show-errors --profile ${testprofile}"
    }
  }
}

task deploy {
    dependsOn assembleTemplate
    outputs.upToDateWhen { false }
	doLast {
		exec {
			commandLine "bash", "-c", "sam deploy --resolve-s3  --template-file ${buildDir}/distributions/formkiq-core/sam/template.yaml --stack-name formkiq-core-${testappenvironment} --parameter-overrides ParameterKey=AppEnvironment,ParameterValue=${testappenvironment} ParameterKey=AdminEmail,ParameterValue=${testadminemail} ParameterKey=EnablePublicUrls,ParameterValue=true --capabilities CAPABILITY_IAM CAPABILITY_AUTO_EXPAND CAPABILITY_NAMED_IAM --region ${testregion} --profile ${testprofile}"
		}
	}
}

task undeploy {
	outputs.upToDateWhen { false }
	doLast {
	  	def group = new ByteArrayOutputStream()

  		// find ApiGatewayInvokeGroup Name
  		exec {
			commandLine "bash", "-c", "aws iam list-groups --profile ${testprofile} | jq -r '.Groups[].GroupName' | grep '^formkiq-core-${testappenvironment}'"
			standardOutput = group
			ignoreExitValue = true
  		}
  		// remove any temp queues.
  		exec {
			commandLine "bash", "-c", "aws sqs list-queues --region ${testregion} --profile ${testprofile} | jq -r '.QueueUrls[]' | grep '/createtest' | xargs -I{} aws sqs delete-queue --queue-url {} --region ${testregion} --profile ${testprofile}"
			ignoreExitValue = true
  		}
  		// remove all users in ApiGatewayInvokeGroup
  		exec {
			commandLine "bash", "-c", "aws iam get-group --group-name ${group.toString().trim()} --profile ${testprofile} | jq -r '.Users[].UserName' | xargs -I{} aws iam remove-user-from-group --group-name ${group.toString().trim()} --user-name {} --region ${testregion} --profile ${testprofile}"
			ignoreExitValue = true
  		}
  		exec {
  			commandLine "bash", "-c", "aws sts get-caller-identity --profile ${testprofile} | jq -r '.Account' | xargs -I{} aws s3api put-bucket-versioning --bucket formkiq-core-${testappenvironment}-staging-{} --versioning-configuration Status=Suspended --region ${testregion} --profile ${testprofile}"
  			ignoreExitValue = true
  		}
  		exec {
  			commandLine "bash", "-c", "aws sts get-caller-identity --profile ${testprofile} | jq -r '.Account' | xargs -I{} aws s3api put-bucket-versioning --bucket formkiq-core-${testappenvironment}-documents-{} --versioning-configuration Status=Suspended --region ${testregion} --profile ${testprofile}"
  			ignoreExitValue = true
  		}
  		
  		// empty buckets
  		exec {
  			commandLine "bash", "-c", "aws sts get-caller-identity --profile ${testprofile} | jq -r '.Account' | xargs -I{} python3 ${project.projectDir}/emptyVersionedBucket.py -b formkiq-core-${testappenvironment}-console-{} -p ${testprofile}"
  			ignoreExitValue = true
  		}
  		exec {
  			commandLine "bash", "-c", "aws sts get-caller-identity --profile ${testprofile} | jq -r '.Account' | xargs -I{} python3 ${project.projectDir}/emptyVersionedBucket.py -b formkiq-core-${testappenvironment}-staging-{} -p ${testprofile}"
  			ignoreExitValue = true
  		}
  		exec {
  			commandLine "bash", "-c", "aws sts get-caller-identity --profile ${testprofile} | jq -r '.Account' | xargs -I{} python3 ${project.projectDir}/emptyVersionedBucket.py -b formkiq-core-${testappenvironment}-documents-{} -p ${testprofile}"
  			ignoreExitValue = true
  		}
  		  		
  		// delete buckets
  		exec {
  			commandLine "bash", "-c", "aws sts get-caller-identity --profile ${testprofile} | jq -r '.Account' | xargs -I{} aws s3 rb s3://formkiq-core-${testappenvironment}-console-{} --force --region ${testregion} --profile ${testprofile}"
  			ignoreExitValue = true
  		}
  		exec {
  			commandLine "bash", "-c", "aws sts get-caller-identity --profile ${testprofile} | jq -r '.Account' | xargs -I{} aws s3 rb s3://formkiq-core-${testappenvironment}-staging-{} --force --region ${testregion} --profile ${testprofile}"
  			ignoreExitValue = true
  		}
  		exec {
  			commandLine "bash", "-c", "aws sts get-caller-identity --profile ${testprofile} | jq -r '.Account' | xargs -I{} aws s3 rb s3://formkiq-core-${testappenvironment}-documents-{} --force --region ${testregion} --profile ${testprofile}"
  			ignoreExitValue = true
  		}
  		// sam delete
  		exec {
			commandLine "bash", "-c", "sam delete --stack-name formkiq-core-${testappenvironment} --no-prompts --region ${testregion} --profile ${testprofile}"
		}
		// delete cloudwatch logs
		exec {
			commandLine "bash", "-c", "aws logs describe-log-groups --log-group-name-prefix /aws/lambda/formkiq-core-${testappenvironment} --region ${testregion} --profile ${testprofile}  | jq -r '.logGroups[].logGroupName' | xargs -I{} aws logs delete-log-group --log-group-name {} --region ${testregion} --profile ${testprofile}"
  		}
  		/*
		exec {
			commandLine "bash", "-c", "aws cloudformation delete-stack --stack-name formkiq-core-${testappenvironment} --region ${testregion} --profile ${testprofile}"
		}
		// delete dynamodb tables
		exec {
			commandLine "bash", "-c", "aws dynamodb list-tables --region ${testregion} --profile ${testprofile} | jq -r '.TableNames[]' | grep '^formkiq-core-${testappenvironment}' | xargs -I {} aws dynamodb delete-table --table-name {} --region ${testregion} --profile ${testprofile}"
		}
		// disable s3 bucket versioning
		exec {
			commandLine "bash", "-c", "aws s3api list-buckets --query \"Buckets[].Name\" --profile ${testprofile} | jq -r '.[]' | grep '^formkiq-core-${testappenvironment}' | xargs -I{} aws s3api put-bucket-versioning --bucket {} --versioning-configuration Status=Suspended --region ${testregion} --profile ${testprofile}"
  		}
  		// delete all s3 objects
  		exec {
	  		commandLine "bash", "-c", "aws s3api list-buckets --query \"Buckets[].Name\" --profile ${testprofile} | jq -r '.[]' | grep '^formkiq-core-${testappenvironment}' | xargs -I {} python3 ${project.projectDir}/emptyVersionedBucket.py -b {} -p ${testprofile}"
	  	}
  		// delete s3 buckets
		exec {
			commandLine "bash", "-c", "aws s3api list-buckets --query \"Buckets[].Name\" --profile ${testprofile} | jq -r '.[]' | grep '^formkiq-core-${testappenvironment}' | xargs -I{} aws s3 rb s3://{} --force --region ${testregion} --profile ${testprofile}"
  		}
  		// delete cloudwatch logs
		exec {
			commandLine "bash", "-c", "aws logs describe-log-groups --log-group-name-prefix /aws/lambda/formkiq-core-${testappenvironment} --region ${testregion} --profile ${testprofile}  | jq -r '.logGroups[].logGroupName' | xargs -I{} aws logs delete-log-group --log-group-name {} --region ${testregion} --profile ${testprofile}"
  		}
  		// wait for stack to be deleted
  		exec {
	  		commandLine "bash", "-c", "aws cloudformation wait stack-delete-complete --stack-name formkiq-core-${testappenvironment} --region ${testregion} --profile ${testprofile}"
		}*/
	}
}

task createSar {
    dependsOn build
    outputs.upToDateWhen { false }
	doLast {
		exec {
			commandLine "bash", "-c", "sam publish --template-file ${buildDir}/distributions/formkiq-core/sar/storage/template.yaml --semantic-version ${project.version} --region ${testregion} --profile ${testprofile}"
		}
		exec {
			commandLine "bash", "-c", "sam publish --template-file ${buildDir}/distributions/formkiq-core/sar/api/template.yaml --semantic-version ${project.version} --region ${testregion} --profile ${testprofile}"
		}
		exec {
			commandLine "bash", "-c", "sam publish --template-file ${buildDir}/distributions/formkiq-core/sar/console/template.yaml --semantic-version ${project.version} --region ${testregion} --profile ${testprofile}"
		}
		exec {
			commandLine "bash", "-c", "sam publish --template-file ${buildDir}/distributions/formkiq-core/sar/module-email-notify/template.yaml --semantic-version ${project.version} --region ${testregion} --profile ${testprofile}"
		}
		exec {
			commandLine "bash", "-c", "sam package --template-file ${buildDir}/distributions/formkiq-core/sar/template.yaml --resolve-s3  --region ${testregion} --profile ${testprofile} --output-template-file build/formkiq-core-sar.yaml"
		}
		exec {
			commandLine "bash", "-c", "sam publish --template-file ${buildDir}/distributions/formkiq-core/sar/template.yaml --semantic-version ${project.version} --region ${testregion} --profile ${testprofile}"
		}
	}
}

task buildSamZip(type: Zip) {
	dependsOn assembleTemplate
	outputs.upToDateWhen { false }
	archiveFileName = "formkiq-core-${project.version}.zip"
    destinationDirectory = file("${buildDir}/distributions/formkiq-core")
   	from("${buildDir}/distributions/formkiq-core/sam") {
   		include '*'
      include '**/**'
    }
	from("${projectDir}") {
   		include 'INSTALL.md'
   		include 'LICENSE'
   		include 'openapi-jwt.yaml'
   		include 'openapi-iam.yaml'
    } 
}
