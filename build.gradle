
plugins {
  id 'java-library'
  id 'checkstyle'
  id 'com.github.spotbugs'  version '4.6.0'
  id 'com.diffplug.spotless' version '5.8.2'
  id 'com.github.ben-manes.versions' version '0.36.0'
  id 'com.formkiq.gradle.graalvm-native-plugin' version '1.0.3'
}

spotless { 
  java { 
    eclipse().configFile project.rootProject.file("spotless.eclipseformat.xml")
    licenseHeaderFile project.rootProject.file("LICENSE")  
  } 
}

allprojects {
  version = '1.0.1'
  group = 'com.formkiq.stacks'
}

subprojects {

  repositories {
  	mavenLocal()
    mavenCentral()
  }

  apply plugin: 'java-library'
  apply plugin: 'checkstyle'
  apply plugin: 'com.github.spotbugs'
  apply plugin: 'com.diffplug.spotless'
  apply plugin: 'com.github.ben-manes.versions'
  apply plugin: 'com.formkiq.gradle.graalvm-native-plugin'
  
  sourceCompatibility = "11"
  targetCompatibility = "11"
  
  compileJava.dependsOn 'spotlessCheck'

  tasks.withType(Test) {
    systemProperties['testregion'] = project.getProperty('testregion')
    systemProperties['testprofile'] = project.getProperty('testprofile')
    systemProperties['testappenvironment'] = project.getProperty('testappenvironment')
  }

  spotlessJavaCheck.dependsOn 'spotlessJavaApply'

  spotbugs {
    excludeFilter = file("$rootDir/config/gradle/spotbugs-exclude.xml")
  }
     
  spotbugsMain {
    reports {
        html {
            enabled = true
        }
    }
  } 
  
  spotbugsTest {
    reports {
        html {
            enabled = true
        }
    }
  } 
  
  checkstyle {
    toolVersion '8.29'
    configFile file("config/checkstyle/checkstyle.xml")
    configProperties = [project_loc: "${projectDir}"]
  }

  tasks.withType(Checkstyle).each { checkstyleTask ->
    checkstyleTask.doLast {
        reports.all { report ->
            def outputFile = report.destination
            if (outputFile.exists() && outputFile.text.contains("<error ")) {
                throw new GradleException("There were checkstyle warnings! For more info check $outputFile")
            }
        }
    }
  }
  
  ext.macroLocalStackStart = {
	exec {
	  commandLine "bash", "-c", "docker-compose  -f ${project.rootDir}/docker-compose.yml up -d localstack"
	}
	exec {
	  commandLine "bash", "-c", "${project.rootDir}/wait-for-localstack.sh"
	}
  }
  
  ext.macroDynamoDbStart = {
  	exec {
	  commandLine "bash", "-c", "docker-compose  -f ${project.rootDir}/docker-compose.yml up -d dynamodb"
	}
	exec {
	  commandLine "bash", "-c", "${project.rootDir}/wait-for-dynamodb.sh"
	}
  }
  
  ext.macroDynamoDbCreateTables = {
	exec {
	  commandLine "bash", "-c", "for f in \$(find ${project.rootDir}/dynamodb-documents/src/test/resources -name \"*.json\"); do aws dynamodb create-table --region ${awsregion} --endpoint=${dynamodbEndpoint} --profile ${awsprofile} --cli-input-json \"\$(cat \$f)\"; done"
	}
  }
  
  ext.macroDynamoDbDeleteAllTables = {
    exec {
      commandLine "bash", "-c", "aws dynamodb list-tables --endpoint=${dynamodbEndpoint} --profile ${awsprofile} --region ${awsregion} | jq -r '.TableNames[]' | xargs -I {} aws dynamodb delete-table --table-name {} --region ${awsregion} --profile ${awsprofile} --endpoint=${dynamodbEndpoint}"
    }
  }
  
  ext.macroDockerComposeDown = {
    exec {
      commandLine "bash", "-c", "docker-compose  -f ${project.rootDir}/docker-compose.yml down"
    }
  }

  task dockerComposeUp {
    doLast {
      macroLocalStackStart()
	  macroDynamoDbStart()
    }
  }

  task dockerComposeDown {
    doLast {
      macroDockerComposeDown()
    }
  }
}

description = "FormKiQ Core"

task testaws {
  dependsOn subprojects.collect { subproject ->
    subproject.tasks.matching { it.name == "testaws" }
  }
}

task cloudFormationPackage {
	doLast {
    	exec {
      		commandLine "bash", "-c", "sam package --template-file ./module-email-notify/src/cloudformation/module.yml --s3-bucket ${distrobucket} --region ${testregion} --profile ${testprofile} --output-template-file build/formkiq-core-module-email-notify.yml"
    	}
		exec {
			commandLine "bash", "-c", "sam package --template-file ./lambda-s3/src/main/resources/cloudformation/data.yml --s3-bucket ${distrobucket} --region ${testregion} --profile ${testprofile} --output-template-file build/formkiq-core-data.yml"
		}
		exec {
			commandLine "bash", "-c", "yq m --overwrite ./lambda-api/src/main/resources/cloudformation/lambda-api.yml ./lambda-api/src/main/resources/cloudformation/api.yml ./lambda-api/src/main/resources/cloudformation/api-iam.yml > ./lambda-api/src/main/resources/cloudformation/lambda-api.yml.tmp ; sam package --template-file ./lambda-api/src/main/resources/cloudformation/lambda-api.yml.tmp --s3-bucket ${distrobucket} --region ${testregion} --profile ${testprofile} --output-template-file build/formkiq-core-api.yml"
		}	
		exec {
			commandLine "bash", "-c", "sam package --template-file ./console/src/main/resources/cloudformation/console.yml --s3-bucket ${distrobucket} --region ${testregion} --profile ${testprofile} --output-template-file build/formkiq-core-console.yml"
		}		
		exec {
			commandLine "bash", "-c", "cp ./src/main/resources/cloudformation/formkiq-core.yml build/formkiq-core.yml"
		}
		exec {
			commandLine "bash", "-c", "box tokenReplace path=build/formkiq-core.yml token=\"{{version}}\" replacement=\"${project.version}\""
		}
		exec {
			commandLine "bash", "-c", "box tokenReplace path=build/formkiq-core.yml token=\"{{aws_accountid}}\" replacement=\"${aws_account_id}\""
		}
		exec {
			commandLine "bash", "-c", "sam package --template-file build/formkiq-core.yml --s3-bucket ${distrobucket} --region ${testregion} --profile ${testprofile} --output-template-file build/formkiq-core.yml"
		}
	}
}

task createApplication {
	doLast {
		exec {
			commandLine "bash", "-c", "sam publish --template-file build/formkiq-core-data.yml --semantic-version ${project.version} --region ${testregion} --profile ${testprofile}"
		}
		exec {
			commandLine "bash", "-c", "sam publish --template-file build/formkiq-core-api.yml --semantic-version ${project.version} --region ${testregion} --profile ${testprofile}"
		}
		exec {
			commandLine "bash", "-c", "sam publish --template-file build/formkiq-core-console.yml --semantic-version ${project.version} --region ${testregion} --profile ${testprofile}"
		}
		exec {
			commandLine "bash", "-c", "sam publish --template-file build/formkiq-core-module-email-notify.yml --semantic-version ${project.version} --region ${testregion} --profile ${testprofile}"
		}
		exec {
			commandLine "bash", "-c", "sam publish --template-file build/formkiq-core.yml --semantic-version ${project.version} --region ${testregion} --profile ${testprofile}"
		}
	}
}
createApplication.dependsOn cloudFormationPackage

task deleteApplication {
	doLast {
		exec {
			commandLine "bash", "-c", "aws serverlessrepo delete-application --application-id arn:aws:serverlessrepo:${testregion}:${aws_account_id}:applications/FormKiQ-Core-Data --region ${testregion} --profile ${testprofile}"
		}
		exec {
			commandLine "bash", "-c", "aws serverlessrepo delete-application --application-id arn:aws:serverlessrepo:${testregion}:${aws_account_id}:applications/FormKiQ-Core-Api --region ${testregion} --profile ${testprofile}"
		}
		exec {
			commandLine "bash", "-c", "aws serverlessrepo delete-application --application-id arn:aws:serverlessrepo:${testregion}:${aws_account_id}:applications/FormKiQ-Core-Console --region ${testregion} --profile ${testprofile}"
		}
		exec {
			commandLine "bash", "-c", "aws serverlessrepo delete-application --application-id arn:aws:serverlessrepo:${testregion}:${aws_account_id}:applications/FormKiQ-Module-Email-Nodify --region ${testregion} --profile ${testprofile}"
		}
		exec {
			commandLine "bash", "-c", "aws serverlessrepo delete-application --application-id arn:aws:serverlessrepo:${testregion}:${aws_account_id}:applications/FormKiQ-Core --region ${testregion} --profile ${testprofile}"
		}		
	}
}

task cloudFormationDelete {
	doLast {
	  	def group = new ByteArrayOutputStream()

  		// find ApiGatewayInvokeGroup Name
  		exec {
			commandLine "bash", "-c", "aws iam list-groups --profile ${testprofile} | jq -r '.Groups[].GroupName' | grep '^formkiq-core-${testappenvironment}'"
			standardOutput = group
			ignoreExitValue = true
  		}
  		// remove all users in ApiGatewayInvokeGroup
  		exec {
			commandLine "bash", "-c", "aws iam get-group --group-name ${group.toString().trim()} --profile ${testprofile} | jq -r '.Users[].UserName' | xargs -I{} aws iam remove-user-from-group --group-name ${group.toString().trim()} --user-name {} --region ${testregion} --profile ${testprofile}"
			ignoreExitValue = true
  		}
		exec {
			commandLine "bash", "-c", "aws cloudformation delete-stack --stack-name formkiq-core-${testappenvironment} --region ${testregion} --profile ${testprofile}"
		}
		// delete dynamodb tables
		exec {
			commandLine "bash", "-c", "aws dynamodb list-tables --region ${testregion} --profile ${testprofile} | jq -r '.TableNames[]' | grep '^formkiq-core-${testappenvironment}' | xargs -I {} aws dynamodb delete-table --table-name {} --region ${testregion} --profile ${testprofile}"
		}
		// disable s3 bucket versioning
		exec {
			commandLine "bash", "-c", "aws s3api list-buckets --query \"Buckets[].Name\" --profile ${testprofile} | jq -r '.[]' | grep '^formkiq-core-${testappenvironment}' | xargs -I{} aws s3api put-bucket-versioning --bucket {} --versioning-configuration Status=Suspended --region ${testregion} --profile ${testprofile}"
  		}
  		// delete all s3 objects
  		exec {
	  		commandLine "bash", "-c", "aws s3api list-buckets --query \"Buckets[].Name\" --profile ${testprofile} | jq -r '.[]' | grep '^formkiq-core-${testappenvironment}' | xargs -I {} ${project.projectDir}/delete_all_s3_object_versions.sh {} ${testprofile}"
	  	}
  		// delete s3 buckets
		exec {
			commandLine "bash", "-c", "aws s3api list-buckets --query \"Buckets[].Name\" --profile ${testprofile} | jq -r '.[]' | grep '^formkiq-core-${testappenvironment}' | xargs -I{} aws s3 rb s3://{} --force --region ${testregion} --profile ${testprofile}"
  		}
  		// delete cloudwatch logs
		exec {
			commandLine "bash", "-c", "aws logs describe-log-groups --log-group-name-prefix /aws/lambda/formkiq-core-${testappenvironment} --region ${testregion} --profile ${testprofile}  | jq -r '.logGroups[].logGroupName' | xargs -I{} aws logs delete-log-group --log-group-name {} --region ${testregion} --profile ${testprofile}"
  		}
  		// wait for stack to be deleted
  		exec {
	  		commandLine "bash", "-c", "aws cloudformation wait stack-delete-complete --stack-name formkiq-core-${testappenvironment} --region ${testregion} --profile ${testprofile}"
		}
	}
}

cloudFormationPackage.dependsOn check