import com.github.jk1.license.filter.LicenseBundleNormalizer
import com.github.jk1.license.render.InventoryHtmlReportRenderer
import com.github.jk1.license.render.JsonReportRenderer
import org.openapitools.generator.gradle.plugin.tasks.ValidateTask

plugins {
  id 'com.formkiq.gradle.java-base' version '1.0.6'
  id 'com.formkiq.gradle.ytt' version '1.0.1'
  id 'de.undercouch.download' version '5.6.0'
  id 'org.openapi.generator' version '7.13.0'
  id 'com.github.jk1.dependency-license-report' version '2.9'
}

repositories { mavenCentral() }

allprojects {

  version = '1.19.0'

  ext.awsCognitoVersion = '1.6.5'
  group = 'com.formkiq.stacks'

  configurations.all {
    resolutionStrategy {
      cacheChangingModulesFor 0, 'seconds'
    }
  }

  licenseReport {
    configurations = ['runtimeClasspath']

    outputDir = "$rootDir/docs/licenses"

    renderers = [
      new JsonReportRenderer('licenses.json'),
      new InventoryHtmlReportRenderer('index.html', 'FormKiQ Core')
    ]
    filters = [
      new LicenseBundleNormalizer()
    ]
  }
}

subprojects {
  tasks.withType(Test).configureEach {
    systemProperties['testregion'] = project.getProperty('testregion')
    systemProperties['testprofile'] = project.getProperty('testprofile')
    systemProperties['testappenvironment'] = project.getProperty('testappenvironment')
    systemProperties['testchatgptapikey'] = project.getProperty('testchatgptapikey')
  }
}

description = "FormKiQ Core"

apply plugin: 'distribution'

tasks.register("downloadAwsCognito", Download) {
  src "https://github.com/formkiq/aws-cognito/releases/download/v${awsCognitoVersion}/aws-cognito-v${awsCognitoVersion}.zip"
  dest(layout.buildDirectory.file("aws-cognito-v${awsCognitoVersion}.zip").get().asFile)
  overwrite false
}

/*
 task downloadAwsCognito(type: Copy) {
 dependsOn jar
 from file("$buildDir/../../../github/aws-cognito/build/aws-cognito-v${awsCognitoVersion}.zip")
 into file("${buildDir}")
 }*/

tasks.register("unzipAwsCognito", Copy) {
  dependsOn downloadAwsCognito
  from({ zipTree(layout.buildDirectory.file("aws-cognito-v${awsCognitoVersion}.zip").get().asFile) })
  into(layout.buildDirectory.dir("modules/cognito"))
}

ytt {
  defaultDataValues.put("version", project.version.toString())

  specs {
    apiLambda {
      from("src/main/resources/cloudformation/api/api-lambda.yaml")
      into("api/api-lambda.yaml")
      hash "sha256"
    }

    api {
      from("build/cloudformation/api/api.yaml")
      into("api/api.yaml")
      hash "sha256"
    }

    apiIam {
      from("build/cloudformation/api/api-iam.yaml")
      into("api/api-iam.yaml")
      hash "sha256"
    }

    apiApikey {
      from("build/cloudformation/api/api-apikey.yaml")
      into("api/api-apikey.yaml")
      hash "sha256"
    }

    apiAuth {
      from("src/main/resources/cloudformation/api/api-auth.yaml",
          "src/main/resources/cloudformation/api/openapi-auth.yaml")
      into("api/api-auth.yaml")
      hash "sha256"
    }

    apiTemplate {
      from("src/main/resources/cloudformation/api/template.yaml",
          "src/main/resources/cloudformation/api/module-extra-core-ocr.yaml")
      into("api/template.yaml")
      hash "sha256"
    }

    ocr {
      from("src/main/resources/cloudformation/api/template-ocr.yaml")
      into("api/template-ocr.yaml")
      hash "sha256"
    }

    typesense {
      from("src/main/resources/cloudformation/typesense/template.yaml")
      into("typesense/template.yaml")
      hash "sha256"
    }

    storage {
      from("src/main/resources/cloudformation/storage/template.yaml")
      into("storage/template.yaml")
      hash "sha256"
    }

    console {
      from("src/main/resources/cloudformation/console/template-install.yaml")
      into("console/template-install.yaml")
      hash "sha256"
    }

    template {
      from("src/main/resources/cloudformation/template.yaml")
      into("template.yaml")
      hash "sha256"
    }
  }
}

tasks.named('distZip') {
  dependsOn subprojects.collect { it.tasks.named('build') }, unzipAwsCognito, yttRenderAll
}

tasks.distTar.enabled = false

distributions {
  main {
    contents {
      duplicatesStrategy = DuplicatesStrategy.FAIL

      from(".") {
        include "INSTALL.md"
        include "LICENSE"
      }
      from("src/main/resources/cloudformation") {
        exclude "api/**"
        exclude "typesense/**"
        exclude "storage/**"
        exclude "console/template-install.yaml"
        exclude "template.yaml"
      }
      from 'build/ytt'
      from 'console/build/distributions'
      from 'build/modules'
      from 'lambda-api-graalvm/build/distributions'
      from 'lambda-s3-graalvm/build/distributions'
      from 'lambda-typesense/build/distributions'
      from 'lambda-ocr-tesseract/build/distributions'
      from 'lambda-apikey-authorizer/build/distributions'
      into '/'
    }
  }
}

tasks.register("validateOpenApiJwtSpec", ValidateTask) {
  inputSpec = "$rootDir/docs/openapi/openapi-jwt.yaml".toString()
  recommend = true
}

tasks.register("validateOpenApiIamSpec", ValidateTask) {
  inputSpec = "$rootDir/docs/openapi/openapi-iam.yaml".toString()
  recommend = true
}

tasks.register("validateOpenApiKeySpec", ValidateTask) {
  inputSpec = "$rootDir/docs/openapi/openapi-key.yaml".toString()
  recommend = true
}

tasks.register("validateOpenApiSpecs") {
  dependsOn(
      "validateOpenApiJwtSpec",
      "validateOpenApiIamSpec",
      "validateOpenApiKeySpec"
      )
}

openApiGenerate {
  generatorName.set("spring")
  inputSpec.set(layout.projectDirectory
      .file("docs/openapi/openapi-jwt.yaml")
      .asFile
      .absolutePath)

  outputDir.set(layout.buildDirectory
      .dir("generated")
      .get()
      .asFile
      .absolutePath)

  apiPackage.set("com.formkiq.springboot.api")
  invokerPackage.set("com.formkiq.springboot.invoker")
  modelPackage.set("com.formkiq.springboot.model")
}

def apiTemplate = "src/main/resources/cloudformation/api/openapi.gotmpl"
def openapiOutDir = "$projectDir/docs/openapi"

def variants = [
  Jwt: [ dirName: "jwt/docs", file:apiTemplate, outputFile:"$projectDir/docs/openapi/openapi-jwt.yaml"],
  JwtCf: [ dirName: "jwt/api", file:"src/main/resources/cloudformation/api/api.gotmpl", template: apiTemplate, outputFile:"build/cloudformation/api/api.yaml"],
  Iam: [ dirName: "iam/docs", file:apiTemplate, outputFile:"$projectDir/docs/openapi/openapi-iam.yaml"],
  IamCf: [ dirName: "iam/api", file:"src/main/resources/cloudformation/api/api-iam.gotmpl", template: apiTemplate, outputFile:"build/cloudformation/api/api-iam.yaml"],
  Key: [ dirName: "key/docs", file:apiTemplate, outputFile:"$projectDir/docs/openapi/openapi-key.yaml"],
  KeyCf: [ dirName: "key/api", file:"src/main/resources/cloudformation/api/api-apikey.gotmpl", template: apiTemplate, outputFile:"build/cloudformation/api/api-apikey.yaml"],
]

variants.each { suffix, cfg ->
  tasks.register("generateOpenApi${suffix}", Exec) {
    dependsOn tasks.named('copyGomplateResources')

    group = "openapi"
    description = "Generate ${suffix} OpenAPI spec using gomplate"

    def pathDir = new File("$buildDir/gomplate", cfg.dirName)

    inputs.file new File(cfg.file)
    inputs.dir pathDir
    outputs.file new File(cfg.outputFile)

    environment "PROJECT_VERSION", project.version.toString()

    def cmd = [
      "gomplate",
      "-f",
      cfg.file,
      "-d",
      "path=${pathDir.absolutePath}${File.separator}",
      "-o",
      cfg.outputFile,
      "-t", "src/main/resources/cloudformation/api/partials"
    ]

    if (cfg.template) {
      cmd += [
        "-t",
        "openapi=${cfg.template}"
      ]
    }

    commandLine cmd
  }
}

tasks.register("generateOpenApiSpecs") {
  group = "openapi"
  description = "Generate all OpenAPI specs using gomplate"
  dependsOn variants.keySet().collect { "generateOpenApi${it}" }
}

generateOpenApiSpecs.dependsOn 'copyGomplateResources'

def yttDeps = [
  "yttRender_apiIam"    : "generateOpenApiIamCf",
  "yttRender_apiApikey" : "generateOpenApiKeyCf",
  "yttRender_api"       : "generateOpenApiJwtCf",
]

tasks.withType(com.formkiq.gradle.AbstractYttRenderTask).configureEach { t ->
  def genTaskName = yttDeps[t.name]
  if (genTaskName != null) {
    t.dependsOn(tasks.named(genTaskName))
  }
}

tasks.register('copyGomplateResources', Copy) {
  description = 'Copy gomplate resources'
  group = 'build'

  into("$buildDir/gomplate")

  from('src/main/resources/cloudformation/api/jwt') {
    into('jwt/docs')
    exclude '**-api.yaml'
    rename('title-docs.yaml', 'title.yaml')
    rename('desc-docs.yaml', 'desc.yaml')
  }

  from('src/main/resources/cloudformation/api/jwt') {
    into('jwt/api')
    exclude '**-docs.yaml'
    rename('title-api.yaml', 'title.yaml')
    rename('desc-api.yaml', 'desc.yaml')
  }

  from('src/main/resources/cloudformation/api/iam') {
    into('iam/docs')
    exclude '**-api.yaml'
    rename('title-docs.yaml', 'title.yaml')
    rename('desc-docs.yaml', 'desc.yaml')
  }

  from('src/main/resources/cloudformation/api/iam') {
    into('iam/api')
    exclude '**-docs.yaml'
    rename('title-api.yaml', 'title.yaml')
    rename('desc-api.yaml', 'desc.yaml')
  }

  from('src/main/resources/cloudformation/api/key') {
    into('key/docs')
    exclude '**-api.yaml'
    rename('title-docs.yaml', 'title.yaml')
    rename('desc-docs.yaml', 'desc.yaml')
  }

  from('src/main/resources/cloudformation/api/key') {
    into('key/api')
    exclude '**-docs.yaml'
    rename('title-api.yaml', 'title.yaml')
    rename('desc-api.yaml', 'desc.yaml')
  }
}
