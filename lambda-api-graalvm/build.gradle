description = "Lambda API Graalvm"

apply plugin: 'distribution'

def getCmd() {
  String os = System.getProperty("os.name").toLowerCase()
  return os.contains("win") ? "cmd" : "bash"
}

def getCmdParam() {
  String os = System.getProperty("os.name").toLowerCase()
  return os.contains("win") ? "/c" : "-c"
}

dependencies {
  implementation project(':lambda-api')
}

compileJava {
  options.annotationProcessorPath += configurations.runtimeClasspath
}

nativeImage {
  outputFileName = "server"
  mainClassName = "com.formkiq.lambda.runtime.graalvm.LambdaRuntime"
  dockerImage = "ghcr.io/graalvm/native-image-community:24"
  enableHttp = true
  enableHttps = true
  buildOptions = "-Os -H:-ReduceImplicitExceptionStackTraceInformation"
  systemProperty = [
    "java.net.preferIPv4Stack=true"
  ]
}

task downloadOpenApiGenerator(type: Download) {
  dependsOn check
  src "https://repo1.maven.org/maven2/org/openapitools/openapi-generator-cli/7.13.0/openapi-generator-cli-7.13.0.jar"
  dest buildDir
  overwrite false
}

task assembleOpenApiTemplate {
  dependsOn downloadOpenApiGenerator
  inputs.files("src/main/resources/cloudformation/api.yaml", "src/main/resources/cloudformation/api-iam.yaml", "src/main/resources/cloudformation/api-key.yaml")

  doLast {

    def fileSpec = new File("${buildDir}/openapi-spec.yaml")
    fileSpec.text = ["Resources:"].join(System.lineSeparator())

    for (fileName in [
          "openapi-jwt",
          "openapi-iam",
          "openapi-key",
          "openapi-auth"
        ]) {
      exec {
        commandLine getCmd(), getCmdParam(), "ytt --data-value version=${project.version} -f ${buildDir}/openapi-spec.yaml -f ${buildDir}/../src/main/resources/cloudformation/${fileName}.yaml > ${buildDir}/temp-${fileName}.yaml"
      }
    }

    for (fileName in [
          "openapi-jwt",
          "openapi-iam",
          "openapi-key",
          "openapi-auth"
        ]) {

      def lines = new File("${buildDir}/temp-${fileName}.yaml").readLines()
      for (int i = 0; i < 4; i++) {
        lines.removeAt(0)
      }

      def apiTitle = fileName == "openapi-jwt" ? "FormKiQ API JWT" :
          fileName == "openapi-iam" ? "FormKiQ API IAM" :
          fileName == "openapi-key" ? "FormKiQ API Key" : "FormKiQ API"

      def file = new File("${buildDir}/../../docs/openapi/${fileName}.yaml")
      file.text = lines
          .findAll { line -> !line.contains("API - ") }
          .collect { e -> e.startsWith("          title:") ? "          title: ${apiTitle}" : e}.join(System.lineSeparator())

      javaexec {
        main="-jar";
        args = [
          "${buildDir}/openapi-generator-cli-7.13.0.jar",
          "generate",
          "-i",
          "${buildDir}/../../docs/openapi/${fileName}.yaml",
          "-g",
          "asciidoc",
          "-o",
          "${buildDir}/${fileName}"
        ]
      }
    }
  }
}

tasks.distTar.enabled = false

distZip {
  archiveFileName = "api/lambda-api-graalvm.zip"
}

distributions {
  main {
    contents {
      from("runtime/") {
        include "bootstrap"
      }
      from ("${buildDir}/graalvm/output") {
        include("server")
      }
      into '/'
    }
  }
}
